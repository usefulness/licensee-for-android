import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.plugin.KotlinPluginWrapperKt

plugins {
    alias(libs.plugins.starter.config)
    alias(libs.plugins.starter.versioning)
    alias(libs.plugins.starter.library.kotlin) apply false
}

commonConfig {
    javaVersion JavaVersion.VERSION_11
}

allprojects {
    pluginManager.withPlugin("kotlin") { plugin ->
        def kotlinVersion = KotlinPluginWrapperKt.getKotlinPluginVersion(project)
        configurations.matching { it.name != "detekt" }.configureEach {
            resolutionStrategy.eachDependency {
                if (requested.group == 'org.jetbrains.kotlin' && requested.name.startsWith("kotlin")) {
                    useVersion kotlinVersion
                }
            }
        }
        kotlin {
            jvmToolchain(libs.versions.java.compilation.get().toInteger())
            compilerOptions {
                apiVersion = KotlinVersion.KOTLIN_1_8
                languageVersion = KotlinVersion.KOTLIN_1_8
            }
        }
    }
    pluginManager.withPlugin("java") {
        if (project.hasProperty("skipJarVersion")) {
            tasks.named("jar") {
                archiveFile.set(project.layout.buildDirectory.map { it.file("libs/${project.name}.jar")})
            }
        }
    }
}
