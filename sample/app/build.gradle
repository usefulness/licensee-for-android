plugins {
    alias(libs.plugins.starter.application.android)
    alias(libs.plugins.app.cash.licensee)
    id("io.github.usefulness.licensee-for-android")
    alias(libs.plugins.starter.easylauncher)
}

licensee {
    allow("Apache-2.0")
}

licenseeForAndroid {
    enableKotlinCodeGeneration = true
    enableResourceGeneration = true
}
android {
    namespace "io.githhub.usefulness.licensee.android.app"

    defaultConfig {
        applicationId = "io.githhub.usefulness.licensee.android.app"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        named("debug") {
            storeFile rootProject.file("keys/debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        named("debug") {
            signingConfig signingConfigs.getByName("debug")
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        named("release") {
            minifyEnabled = true
            signingConfig signingConfigs.getByName("debug")
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            matchingFallbacks += ["debug"]
        }
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    testOptions {
        unitTests.includeAndroidResources true
    }
    lint {
        checkReleaseBuilds = false
    }
    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.androidx.compose.compiler.get()
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}
tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

dependencies {
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.core.ktx)
    implementation(libs.lifecycle.runtime.ktx)
    implementation(libs.activity.compose)
    implementation(libs.ui)
    implementation(libs.ui.graphics)
    implementation(libs.material3)

    implementation(project(":ui"))

    //noinspection UseTomlInstead, see `PluginIntegrationTest`
    debugImplementation("androidx.viewpager2:viewpager2:1.1.0")
    debugImplementation(libs.ui.tooling.preview)

    testRuntimeOnly(libs.junit.platform.launcher)
    testRuntimeOnly(libs.junit.vintage.engine)

    testImplementation(libs.junit4)
    testImplementation(libs.assertj.core)
    testImplementation(libs.androidx.test.core)
    testImplementation(libs.org.robolectric.core)
    testImplementation(libs.kotlinx.serialization.json.okio)
}
